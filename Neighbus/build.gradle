plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.neighbus'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

ext {
    // spring-ai BOM 버전
    set('springAiVersion', "1.1.0")
}

// ★ [추가된 부분] 컴파일 시 메서드 파라미터 이름을 class 파일에 남기도록 설정
// 이 설정이 있어야 컨트롤러에서 @RequestParam("name") 생략 가능
tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters'
}

dependencies {
    /* ---------------------- Spring Boot 기본 ---------------------- */
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'net.nurigo:sdk:4.3.0'
	implementation "software.amazon.awssdk:s3:2.25.55"
	
	
	
    // Mac DNS 이슈 해결용 (M1/M2/M3 등 Apple Silicon인 경우)
	runtimeOnly 'io.netty:netty-resolver-dns-native-macos::osx-aarch_64'

    /* ---------------------- Thymeleaf Security ---------------------- */
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'

    /* ---------------------- Spring AI (신버전 M2) ---------------------- */
    implementation 'org.springframework.ai:spring-ai-starter-model-openai'

    /* ---------------------- MyBatis + MySQL ---------------------- */
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'
    runtimeOnly 'com.mysql:mysql-connector-j'

    /* ---------------------- 개발 편의 ---------------------- */
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    /* ---------------------- 테스트 ---------------------- */
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.3'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    /* ---------------------- Lombok ---------------------- */
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}


dependencyManagement {
    imports {
        // spring-ai 버전 통일
        mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
    }
}
/*서버 배포시 java폴더에 있는 *.xml파일은 날라가서 챙겨가라고 지정*/
processResources {
    from('src/main/java') {
        include '**/*.xml'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

task encodePassword(type: JavaExec) {
    description = '비밀번호를 암호화합니다.'
    group = 'application'
    mainClass = 'com.neighbus.util.PasswordEncoderUtil'
    classpath = sourceSets.main.runtimeClasspath
}